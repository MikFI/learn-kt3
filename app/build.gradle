plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    //kotlin annotation processing tool
    //для кодогенерации
    //kotlinlang рекомендует использовать Kotlin Symbol Processing API,
    //поскольку kapt находится в maintenance mode и не будет поддерживать
    //новый функционал, когда он появится
//    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "ru.netology.nmedia"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures.viewBinding = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def lifecycle_version = "2.5.1"
    def nav_version = "2.5.3"
    def room_version = "2.4.3"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // optional - Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    // activity
    implementation "androidx.activity:activity-ktx:1.5.1"

    //для отображения элементов списком
    implementation("androidx.recyclerview:recyclerview:1.2.1")

    //для работы с JSON-форматом
    implementation("com.google.code.gson:gson:2.9.1")

    //для работы с фрагментами
    implementation "androidx.fragment:fragment-ktx:1.5.4"

    //для навигации между фрагментами
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //для работы с SQL
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-common:$room_version"
//    kapt "androidx.room:room-compiler:2.4.3"
    ksp "androidx.room:room-compiler:$room_version"

    //для пушей
    //bom (bill of materials) - агрегатор версий, чтобы не указывать версию руками
    //для пакетов firebase, указанных ниже - она сама следит за обновлениями,
    //а также за зависимостями (на случай несовместимости версий каких-то библиотек)
    implementation platform('com.google.firebase:firebase-bom:31.0.0')
    implementation "com.google.firebase:firebase-messaging-ktx"
}